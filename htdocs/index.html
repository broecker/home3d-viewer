<!DOCTYPE html>
<html>
<head>
	<title>Hello Pointcloud!!</title>

	<!-- look here: http://css3buttongenerator.com/ -->

	<style>
	html, body {
		padding: 0px;
		margin: 0px;
		width: 100%;
		height: 100%;
		overflow: hidden;
	}

	.btn {
		background:rgba(52, 152,219, 0.4);
		font-family: Arial;
		color: #ffffff;
		font-size: 15px;
		border: solid #c40c40 0px;
		padding: 6px 20px 6px 20px;
	}

	.btn:hover {
		background:rgba(60,176,253, 0.9);
		text-decoration: none;
	}

	.buttonBar { 
		float:left;
		position: absolute;
		left: 10px;
		top: 10px;
		z-index: 100;
	}

	.buildInfo { 
		float:left;
		position:absolute;
		left: 10px;
		bottom:10px;
		color:#f0f0f0;
		font-size: 10px;
	}


	
	canvas {
		position: relative;
		width: 100%;
		height: 100%;
		z-index: 0;
	}
	
	


	</style>

	<meta charset="UTF-8">
	<meta name="viewport" content="width=device-width">
	<meta name="viewport" content="width=device-width, initial-scale=1">


	<script id="pointVS" type="x-shader/x-vertex">
		attribute vec3 positionIn;
		attribute vec3 colorIn;

		uniform mat4 viewMatrix;
		uniform mat4 projMatrix;

		uniform float pointSize;
		uniform float viewportHeight;

		varying vec3 color;
		void main()
		{
			gl_Position = projMatrix * viewMatrix * vec4(positionIn, 1.0);

			color = colorIn;
				
			// conversion factor converts from m to cm
			gl_PointSize = (viewportHeight * pointSize * 0.01) / gl_Position.w;
			
		}
	</script>
	
	<script id="pointFS" type="x-shader/x-fragment">
		precision mediump float;

		varying vec3 color;
		
		void main()
		{
			// this enables round point sprites in OpenGL ES2			
			vec2 pt = gl_PointCoord - vec2(0.5);
			if(dot(pt, pt) > 0.25)
    			discard;

			gl_FragColor = vec4(color, 1.0);
		}
	</script>
	
	
	<script id="gridVS" type="x-shader/x-vertex">
	  attribute vec3 positionIn;
	  
	  uniform mat4 viewMatrix;
	  uniform mat4 projMatrix;
	  
	  void main()
	  {
	    gl_Position = projMatrix * viewMatrix * vec4(positionIn, 1.0);
	    gl_PointSize = 1.0;
	    
	  }
	</script>
	
	<script id="gridFS" type="x-shader/x-fragment">
	  precision mediump float;
	  uniform vec3 color;
	  
	  void main()
	  {
	    gl_FragColor = vec4(color, 1.0);
	  }
	</script>

	<script id="objectVS" type="x-shader/x-vertex">
	  attribute vec3 positionIn;
	  attribute vec3 colorIn;

	  uniform mat4 viewMatrix;
	  uniform mat4 projMatrix;
	  
	  varying vec3 color;

	  void main()
	  {
	    gl_Position = projMatrix * viewMatrix * vec4(positionIn, 1.0);
	    color = colorIn;
	  }
	</script>


	<script id="objectFS" type="x-shader/x-fragment">
	  precision mediump float;
	  varying vec3 color;
	  
	  void main()
	  {
	    gl_FragColor = vec4(color, 1.0);
	  }
	</script>	


	<script id="boundsVS" type="x-shader/x-vertex">
		attribute vec2 positionIn;
		void main()
		{
			gl_Position = vec4(positionIn, 0.0, 1.0);
		}
	</script>

	<script id="boundsFS" type="x-shader/x-fragment">
		precision highp float;
		uniform float projectedArea;
		uniform float pointsContained;

		void main()
		{
			vec3 red = vec3(1.0, 0.0, 0.0);
			vec3 grn = vec3(0.0, 1.0, 0.0);

			float density = sqrt(pointsContained / projectedArea);

			vec3 color = mix(grn, red, density);
			gl_FragColor = vec4(color, 1.0);
		}

	</script>

	<script id="quadVS" type="x-shader/x-vertex">

		attribute vec2 positionIn;
		varying vec2 texcoords;

		void main()
		{

			gl_Position = vec4(positionIn*2.0 - vec2(1.0), 0.0, 1.0);
			texcoords = positionIn.xy;

		}
	</script>

	<script id = "quadFS" type="x-shader/x-fragment">
		precision mediump float;

		uniform sampler2D colormap;
		varying vec2 texcoords;


		void main()
		{
			gl_FragColor = texture2D(colormap, texcoords);

		}

	</script>

	<script id="fxaaVS" type="x-shader/x-vertex">

		precision mediump float;

		attribute vec2 positionIn;
		varying vec2 texcoords;

		//texcoords computed in vertex step
		//to avoid dependent texture reads
		varying vec2 v_rgbNW;
		varying vec2 v_rgbNE;
		varying vec2 v_rgbSW;
		varying vec2 v_rgbSE;
		varying vec2 v_rgbM;

		uniform vec2 resolution;


		//To save 9 dependent texture reads, you can compute
		//these in the vertex shader and use the optimized
		//frag.glsl function in your frag shader.
		//This is best suited for mobile devices, like iOS.
		void calculateTexcoords(vec2 fragCoord, vec2 resolution,
			out vec2 v_rgbNW, out vec2 v_rgbNE,
			out vec2 v_rgbSW, out vec2 v_rgbSE,
			out vec2 v_rgbM) {
			vec2 inverseVP = 1.0 / resolution.xy;
			v_rgbNW = (fragCoord + vec2(-1.0, -1.0)) * inverseVP;
			v_rgbNE = (fragCoord + vec2(1.0, -1.0)) * inverseVP;
			v_rgbSW = (fragCoord + vec2(-1.0, 1.0)) * inverseVP;
			v_rgbSE = (fragCoord + vec2(1.0, 1.0)) * inverseVP;
			v_rgbM = vec2(fragCoord * inverseVP);
		}

		void main()
		{

			gl_Position = vec4(positionIn*2.0 - vec2(1.0), 0.0, 1.0);
			texcoords = positionIn.xy;

			//compute the texture coords and send them to varyings
			vec2 fragCoord = texcoords * resolution;
			calculateTexcoords(fragCoord, resolution, v_rgbNW, v_rgbNE, v_rgbSW, v_rgbSE, v_rgbM);

		}
	</script>

	<script id = "fxaaFS" type="x-shader/x-fragment">
		precision mediump float;

		uniform sampler2D colormap;
		varying vec2 texcoords;

		varying vec2 v_rgbNW;
		varying vec2 v_rgbNE;
		varying vec2 v_rgbSW;
		varying vec2 v_rgbSE;
		varying vec2 v_rgbM;

		//make sure to have a resolution uniform set to the screen size
		uniform vec2 resolution;


		/**
		Basic FXAA implementation based on the code on geeks3d.com with the
		modification that the texture2DLod stuff was removed since it's
		unsupported by WebGL.
		-- 
		From:
		https://github.com/mitsuhiko/webgl-meincraft
		Copyright (c) 2011 by Armin Ronacher.
		Some rights reserved.
		Redistribution and use in source and binary forms, with or without
		modification, are permitted provided that the following conditions are
		met:
		* Redistributions of source code must retain the above copyright
		notice, this list of conditions and the following disclaimer.
		* Redistributions in binary form must reproduce the above
		copyright notice, this list of conditions and the following
		disclaimer in the documentation and/or other materials provided
		with the distribution.
		* The names of the contributors may not be used to endorse or
		promote products derived from this software without specific
		prior written permission.
		THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
		"AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
		LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
		A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
		OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
		SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
		LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
		DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
		THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
		(INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
		OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
		*/
		#ifndef FXAA_REDUCE_MIN
		#define FXAA_REDUCE_MIN (1.0/ 128.0)
		#endif
		#ifndef FXAA_REDUCE_MUL
		#define FXAA_REDUCE_MUL (1.0 / 8.0)
		#endif
		#ifndef FXAA_SPAN_MAX
		#define FXAA_SPAN_MAX 8.0
		#endif
		//optimized version for mobile, where dependent
		//texture reads can be a bottleneck
		vec4 fxaa(sampler2D tex, vec2 fragCoord, vec2 resolution,
			vec2 v_rgbNW, vec2 v_rgbNE,
			vec2 v_rgbSW, vec2 v_rgbSE,
			vec2 v_rgbM) {
			vec4 color;
			mediump vec2 inverseVP = vec2(1.0 / resolution.x, 1.0 / resolution.y);
			vec3 rgbNW = texture2D(tex, v_rgbNW).xyz;
			vec3 rgbNE = texture2D(tex, v_rgbNE).xyz;
			vec3 rgbSW = texture2D(tex, v_rgbSW).xyz;
			vec3 rgbSE = texture2D(tex, v_rgbSE).xyz;
			vec4 texColor = texture2D(tex, v_rgbM);
			vec3 rgbM = texColor.xyz;
			vec3 luma = vec3(0.299, 0.587, 0.114);
			float lumaNW = dot(rgbNW, luma);
			float lumaNE = dot(rgbNE, luma);
			float lumaSW = dot(rgbSW, luma);
			float lumaSE = dot(rgbSE, luma);
			float lumaM = dot(rgbM, luma);
			float lumaMin = min(lumaM, min(min(lumaNW, lumaNE), min(lumaSW, lumaSE)));
			float lumaMax = max(lumaM, max(max(lumaNW, lumaNE), max(lumaSW, lumaSE)));
			mediump vec2 dir;
			dir.x = -((lumaNW + lumaNE) - (lumaSW + lumaSE));
			dir.y = ((lumaNW + lumaSW) - (lumaNE + lumaSE));
			float dirReduce = max((lumaNW + lumaNE + lumaSW + lumaSE) *
			(0.25 * FXAA_REDUCE_MUL), FXAA_REDUCE_MIN);
			float rcpDirMin = 1.0 / (min(abs(dir.x), abs(dir.y)) + dirReduce);
			dir = min(vec2(FXAA_SPAN_MAX, FXAA_SPAN_MAX),
			max(vec2(-FXAA_SPAN_MAX, -FXAA_SPAN_MAX),
			dir * rcpDirMin)) * inverseVP;
			vec3 rgbA = 0.5 * (
			texture2D(tex, fragCoord * inverseVP + dir * (1.0 / 3.0 - 0.5)).xyz +
			texture2D(tex, fragCoord * inverseVP + dir * (2.0 / 3.0 - 0.5)).xyz);
			vec3 rgbB = rgbA * 0.5 + 0.25 * (
			texture2D(tex, fragCoord * inverseVP + dir * -0.5).xyz +
			texture2D(tex, fragCoord * inverseVP + dir * 0.5).xyz);
			float lumaB = dot(rgbB, luma);
			if ((lumaB < lumaMin) || (lumaB > lumaMax))
			color = vec4(rgbA, texColor.a);
			else
			color = vec4(rgbB, texColor.a);
			return color;
		}


		void main()
		{
			mediump vec2 fragCoord = texcoords * resolution; 
			gl_FragColor = fxaa(colormap, fragCoord, resolution, v_rgbNW, v_rgbNE, v_rgbSW, v_rgbSE, v_rgbM);

		}

	</script>


	<script id="skyboxVS" type="x-shader/x-vertex">
		attribute vec2 positionIn;
		varying vec2 texcoords;
		varying vec3 viewDir;

		uniform mat4 inverseMVP;

		void main()
		{

			vec4 worldPos = inverseMVP * vec4(positionIn, 1.0, 1.0);
			worldPos /= worldPos.w;

			vec4 cameraPos = inverseMVP * vec4(0.0, 0.0, 0.0, 1.0);
			cameraPos /= cameraPos.w;

			viewDir = worldPos.xyz - cameraPos.xyz; 

			gl_Position = vec4(positionIn*2.0 - vec2(1.0), 0.0, 1.0);
			texcoords = positionIn.xy;

		}
	</script>

	<script id = "skyboxFS" type="x-shader/x-fragment">
		precision mediump float;

		varying vec2 texcoords;
		varying vec3 viewDir;


		void main()
		{
			vec3 V = normalize(viewDir);

			vec3 horizonColor = vec3(0.8, 0.8, 0.9);
			vec3 bottomColor = vec3(0.3, 0.3, 0.4);
			vec3 topColor = vec3(0.7, 0.7, 1.0);

			vec3 color = V;
			if (V.y > 0.0)
				color = mix(horizonColor, topColor, V.y);
			else
				color = mix(horizonColor, bottomColor, -V.y);

			gl_FragColor = vec4(color, 1.0);

		}

	</script>


	<script src="scripts/gl-matrix.js"></script>
	<script src="scripts/aabb.js"></script>
	<script src="scripts/camera.js"></script>
	<script src="scripts/octree.js"></script>
	<script src="scripts/ray.js"></script>
	<script src="scripts/stats.js"></script>
	<script src="scripts/shader.js"></script>
	<script src="scripts/geometry.js"></script>
	<script src="scripts/dat-gui.js"></script>
	<script src="scripts/framebuffer.js"></script>
	<script src="scripts/main.js"></script>
	<script src='scripts/nprogress.js'></script>

	<link rel='stylesheet' href='styles/nprogress.css'/>

</head>
	<body onload="main('http://10.129.29.12:8000/bear/bear4.json')" >
	<canvas id="canvas" width="800" height="600"></canvas>

	<div class="buttonBar">
		<button type="button" class="btn" onclick="toggleGrid()">Toggle Grid</button>
		<button type="button" class="btn" onclick="toggleBBox()">Toggle BBox</button>
		<button type="button" class="btn" onclick="resetCamera()">Reset Camera</button>
		<button type="button" class="btn" onclick="increaseDetail()">Increase Detail</button>
		<button type="button" class="btn" onclick="decreaseDetail()">Decrease Detail</button>
		<!-- <button type="button" class="btn" onclick="showhelp()">Help</button> -->
	</div>

	<div class="buildInfo">Build: 20150325</div>
</body>
</html>